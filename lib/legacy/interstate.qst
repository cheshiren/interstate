:init
cls
perkill
invkill
gametitle = "Интерстейт"

decoradd back (10,40,10) rect 780,550,0xfff8dc
decoradd fade (10,40,-1) rect 780,550,0x01ffffff
decoradd shop (30,55,2) text "", 0x222222
decor_shop_width = 740
decor_shop_hide = 1
decoradd spot (0,0,1) image "spot.png"
decor_spot_hide = 1
decoradd plan (0,0,-1) image "plan.png"
decor_plan_hide = 1
decor_plan_color = 0x01ffffff
textcolor = 0x7f6500
textpane_left = 30
textpane_width = 740

сиг = 1 + rnd17
курение = 5
пить = 7
флешбекбезтравки = 4
вмашине = 1
вдвижении = 1
нагревание = 0
виделбак = 0
былфлешбек = 0
гденаазс = 0
машинанаазс = 0
ящикоткрыт = 0
окноразбито = 0
складоткрыт = 0
фонарьвкл = 0
знаниеозапахе = 0
былсветвмагазине = 0
патрубок = 0
вес = 0
вода = 0
сполок = 0
продукты = 0
усталость = 0
рюкзакубрал = 0
виделтетрадь = 0
instr пачка = 
instr код = 
номер_трека = 1
громкостьвмашине = music_volume
цветшрифта = textcolor
instr трек1 = Pink Floyd - Hey you
instr трек2 = Jefferson Airplane - White Rabbit
instr трек3 = The Doors - The End
instr трек4 = System of a Down - Lonely Day
instr описание = 

music #%трек#номер_трека$$.mp3

Use_радио_Включить_hide = 1
Use_бардачок_Взять сигарету_hide = 1
Use_урна_Коснуться_hide = 1
Use_машина_Oстановиться_hide = 1
Use_машина_Выйти_hide = 1
Use_машина_Внутрь_hide = 1
Use_машина_Идти к багажнику_hide = 1
Use_машина_Идти к водительской двери_hide = 1
Use_машина_Переехать на пункт налива_hide = 1
Use_машина_Ехать дальше_hide = 1
Use_багажник_Открыть холодильник_hide = 1
Use_багажник_Взять воду_hide = 1
Use_багажник_Взять рюкзак_hide = 1
Use_бак_Осмотреть_hide = 1
Use_бак_Пнуть_hide = 1
Use_азс_Прочесть записку_hide = 1
Use_азс_Осмотреть насос_hide = 1
Use_азс_Осмотреть ящик_hide = 1
Use_азс_Влезть в окно_hide = 1
Use_азс_Вылезти через окно_hide = 1
Use_азс_Идти под навес_hide = 1
Use_азс_Идти к южной стороне здания_hide = 1
Use_азс_Идти к пункту налива_hide = 1
Use_азс_Пройти в магазин_hide = 1
Use_азс_Идти на склад_hide = 1
Use_азс_Выйти на улицу_hide = 1
Use_азс_Пройти через дверь в магазин_hide = 1
Use_азс_Пройти на склад_hide = 1
Use_азс_Идти мимо холодильников_hide = 1
Use_азс_Идти вдоль конторки_hide = 1
Use_азс_Идти вдоль витрины_hide = 1
Use_азс_Идти мимо стеллажей_hide = 1
;Use_азс_Осмотреться_hide = 1
Use_азс_Войти в дверь_hide = 1
Use_азс_Выйти на улицу_hide = 1
Use_азс_Идти в темноту_hide = 1
Use_рюкзак_Выключить фонарь_hide = 1
Use_рюкзак_Снять респиратор_hide = 1
Use_рюкзак_Освободить рюкзак_hide = 1
Use_патрубок_Начать откачивать бензин_hide = 1
Use_Шланг_Начать откачивать бензин_hide = 1
Use_Компрессор_Начать откачивать бензин_hide = 1
Use_Заднее сиденье_Искать обезболивающее_hide = 1

proc датаикод

goto старт
end

;---------------------------------------------------------------------------------------------------

:старт
hide_pause_indicator = 1
decoradd last (0,0,-20) rect 800,600,0xff000000
decorcol last 0x01000000, 2000
pause 2500
decorcol fade 0xfffff8dc
decorcol fade 0x01fff8dc, 500
pln 
pln 
pln 
pln 
pln 
pln 
pln 
pln 
pln 
textalign = 3
textfont = "archangelsk.fnt"
pln ИНТЕРСТЕЙТ
textalign = 1
textfont = "_sysfont"
anykey
decorcol fade 0xfffff8dc, 500
pause 500
goto шоссе1
end

:шоссе1

inv+ машина
inv+ радио
inv+ бардачок
inv+ урна

idisp_машина = "Trans Am"
idisp_радио = "Приемник"
idisp_бардачок = "Бардачок"
idisp_урна = "Урна"

cls
decorcol fade 0x01fff8dc, 500
pln 	Пейзаж уже много часов остается неизменным. Плоская, от горизонта до горизонта, степь с высохшими кустами, сизая полоска гор на западе, идеально прямая лента шоссе и мелькающие вдоль обочины опоры линий электропередач.
pln 	Мотор урчит низко и ровно, Транс-Эм отлично держит дорогу. Я себя не сдерживаю, стрелка спидометра указывает на значение, раза в полтора превышающее допустимый здесь скоростной предел.
pln 	Солнце палит с самой вершины блекло-голубого неба. Встречный ветер треплет рукав рубашки и безуспешно пытается заглушить мотор и музыку.
pause 500
decorcol fade 0x01fffffff
proc события
end

:флешбэк1
cls
decor_back_color = 0x8b96a1
textcolor = 0x600000
pln 	Их было трое против одного.
pln 	Они - здоровые тупые быки из старших классов, он - высокий, худой, с копной черных волос и раскосыми глазами.
pln 	"Слышь, узкоглазый, ты бы валил в свой Китай."
pln 	"Я кореец, идиот. И я живу здесь всю жизнь. Так что можешь засунуть свой Китай себе в зад."
pln 	"Что ты сказал?"
pln 
pln 	Не лезь в чужую драку. Мне всегда об этом говорили, прямо или косвенно. Никогда не лезь в чужую драку.
pln 	Я и не хотел. Если бы этот переулок не был моим единственным путем из школы домой, меня бы вообще там не было.
pln 	Да и что я мог, тощий заучка с длинными пальцами и застегнутой на все пуговицы рубашкой?
pln 	Мда.
pln
pln 	Уже потом, мы сидели бок о бок, прислонившись к стене, и затыкали разбитые носы моим оторванным рукавом.
pln 	"Я Ким," - он протянул свою узкую ладонь.
pln 	"Макс," - я пожал ее, походя заметив свои сбитые костяшки.
pln 	"Зачем полез-то? Не твое ж дело."
pln 	Я неопределенно мотнул головой.
pln 	"Ну... спасибо, все равно."
pln 	"А разве Ким не девчоночье имя?" - спросил я через пару минут молчания.
pln 	"Еще раз такое скажешь, я тебя похлеще них отметелю."
pln 	"Ну, у меня просто сестру так двоюродную зовут."
pln 	"В последний раз предупреждаю: завязывай."
pln
pln 	Мы еще немного посидели и, собрав раскиданные рюкзаки, поплелись на выход из переулка.
pln 	В тот день у меня появился лучший друг.
decorcol fade 0x01ffffff, 2000
pause 2000
номер_трека = 2
music #%трек#номер_трека$$.mp3
anykey
decorcol fade 0xffffffff, 2000
былфлешбек = 1
pause 2000
goto шоссе2
end

:шоссе2
cls
decor_back_color = 0xfff8dc
textcolor = 0x7f6500
pln 	Черная лента шоссе бежит под колесами, подтягивая горы все ближе. Все тот же унылый, выжженный пейзаж.
pln 	На обочине мелькает указатель, сообщающий о том, что где-то впереди будет автозаправка. Я смотрю на шкалу количества бензина в баке. Стрелка приближается к "Е".
pln 	Придется заехать.
pln 	Кроме того, хочется пить. Солнце палит нещадно, бутылку с водой я допил минут пятнадцать назад. Нужно остановиться и взять из багажника еще.
decorcol fade 0x01ffffff, 2000
fademusic громкостьвмашине, 2000
;music #%трек#номер_трека$$.mp3, 2000
pause 2000
inv+ машина
inv+ радио
inv+ бардачок
inv+ урна
Use_машина_Oстановиться_hide = 0
пить = 0
end

:удвери
fademusic громкостьвмашине/2, 500
inv- багажник
pln 	Жара невыносимая. Раскаленный, чуть податливый асфальт печет сквозь подошвы. Прикрыв глаза ладонью, я смотрю сначала в ту сторону, откуда приехал, потом в ту, куда еще поеду. Ничего кроме клубящегося марева.
if Use_радио_Выключить_hide = 1 or music_volume = 0 then pln 	И осязаемая, тягучая тишина, нарушаемая только потрескиванием остывающего мотора.
proc события
Use_машина_Внутрь_hide = 0
Use_машина_Идти к багажнику_hide = 0
Use_машина_Идти к водительской двери_hide = 1
end

:флешбэк2
cls
decor_back_color = 0x000000
textcolor = 0xa7c499
pln 	"...Не даст она тебе, Макс."
pln 	"Даст, вот увидишь."
pln 	"Да ты посмотри на нее. Маленькая, бледная, в очочках. Что ты там увидел?"
pln 	"Ничего ты не понимаешь, Ким. У меня, может, чувство?"
pln 	"Напомню, в прошлый раз твое чувство обошлось тебе в два сломанных ребра."
pln 	"Нет, в этот раз все по-настоящему."
pln 	"Ну еще бы. Ставлю свою стипендию, что у тебя ничего не выгорит."
pln 	"Заметано."
pln 	Мы лежали на белоснежных шезлонгах посреди леса, смотрели на звезды над верхушками сосен и курили один на двоих джойнт. Со стороны основного лагеря все еще доносились голоса и музыка, но уже тише - все постепенно расходились по палаткам и трейлерам. Вскоре оттуда доносилось лишь слабое треньканье - кому-то не спалось, как и нам.
pln
pln 	"Смотри, вон те четыре звезды и вон те три, вместе похожи на лист конопли. Видишь?"
pln 	"Тебе, похоже, уже хватит. Давай, передавай."
pln 	И в этот момент небо прочертил метеорит.
pln 	"Видел? Звезда упала!"
pln 	"Да, надо желания загадывать."
pln 	"Так-так, что бы загадать? Так... а, вот! Все, я загадал, а ты?"
pln 	"Еще раньше тебя."
pln 	"Ага, конечно. И что ты загадал?"
pln 	"Не скажу, не сбудется."
pln 	"Ладно, Ким, не ломайся, что загадал-то?"
pln 	"Ты же ржать будешь."
pln  	"Не буду, честно."
pln 	"...Я хочу увидеть океан."
pln 	Я продержался секунд пять прежде, чем засмеятся:
pln  	"Океааан?"
pln 	"Ну ты и козел, Макс."
pln 	"Да кто загадывает увидеть океан? Что это за фигня?"
pln 	
pln 	Я давился еще несколько минут, пока не успокоился.
pln 	"Ладно, Ким, извини. Нормальное желание на самом деле. Я бы тоже непрочь посмотреть на океан. Да не дуйся ты. Хочешь, я скажу тебе, что я загадал?"
pln 	"Я и так знаю, что ты загадал. Только она тебе все равно не даст."
pln 	Сначала смешок, потом еще, и мы уже катались на своих шезлонгах, задыхаясь и заливаясь слезами от смеха.
pln 
pln 	"Фуух, вроде отпустило."
pln 	"Есть охота."
pln 	"Ага. Пошли, сожрем чей-нибудь завтрак."
pln 	И мы поплелись в сторону лагеря, таща за собой шезлонги.
pln 	Мое желание исполнилось той же ночью.
decorcol fade 0x01ffffff, 2000
pause 2000
номер_трека = 3
music #%трек#номер_трека$$.mp3
anykey
decorcol fade 0xffffffff, 2000
былфлешбек = 2
курение = 5
pause 2000
goto шоссе3
end

:шоссе3
cls
decor_back_color = 0xfff8dc
textcolor = 0x7f6500
pln 	Горы придвинулись еще сильнее, уже видно снег на их вершинах. Солнце откатилось на западную половину неба, но печет все так же неистово.
pln 	Судя по одометру, до заправочной станции остается совсем немного. Скоро на горизонте появляется светлое пятно. Оно постепенно увеличивается и через некоторое время уже можно различить цены на высокой стойке у въезда, шесть колонок, стоящих под навесом, и опущенные металлические ставни небольшого супермаркета.
decorcol fade 0x01ffffff, 2000
fademusic громкостьвмашине, 2000
;music #%трек#номер_трека$$.mp3
pause 2000
anykey
pln 
pln 	Я въезжаю на территорию заправки, медленно объезжаю ее по периметру и, наконец, останавливаюсь в тени под навесом.
pln 	Итак. Заправиться от колонок не удастся, потому что нет электричества, а автономного генератора я нигде не увидел. Поэтому единственный выход - добраться до подземных резервуаров через наливную систему. Пункт налива я заметил на площадке за зданием магазина, нужно будет подогнать Транс-Эм туда, когда разберусь с проблемой доступа к топливу, а пока пусть постоит в тени.
pln 	Кроме того, в самом магазине может быть много необходимых вещей - консервы, вода, сигареты.
pln 	Пора доставать из багажника рюкзак с моим "набором мародера" и отправляться за покупками.
inv+ машина
inv+ радио
inv+ бардачок
inv+ урна
inv+ бутылка
if Use_пакет_Оставить себе_hide = 1 then inv+ пакет
вдвижении = 0
машинанаазс = 1
гденаазс = 1
Use_машина_Выйти_hide = 0
end

:удвери2
fademusic громкостьвмашине/2, 500
inv- багажник
гденаазс = 1
proc осмотразс
Use_машина_Внутрь_hide = 0
Use_машина_Идти к багажнику_hide = 0
Use_машина_Идти к водительской двери_hide = 1
end

:удвери3
fademusic громкостьвмашине/2, 500
гденаазс = 6
proc осмотразс
Use_машина_Внутрь_hide = 0
Use_азс_Осмотреть насос_hide = 0
if ящикоткрыт = 0 then Use_азс_Осмотреть ящик_hide = 0
if ящикоткрыт = 1 then inv+ патрубок
if шлангнапункте = 1 and патрубок <> 2 then inv+ Шланг
if компресснапункте = 1 and патрубок <> 2 then inv+ Компрессор
Use_азс_Обойти здание с восточной стороны_hide = 1
Use_азс_Обойти здание с западной стороны_hide = 1
Use_азс_Идти к южной стороне здания_hide = 0
Use_азс_Идти к главному фасаду_hide = 1
if продукты = 1 and вода = 2 and сполок = 1 and патрубок = 2 then pln 	Я сделал все, что хотел, теперь можно ехать. & end
if патрубок = 2 then if продукты <> 1 or вода <> 2 or сполок <> 1 then pln 	Нужно проверить, что еще можно собрать в магазине, и можно ехать. & end
if продукты <> 1 and вода <> 2 and сполок <> 1 and патрубок = 2 then pln 	Нужно проверить, что можно собрать в магазине, и можно ехать. & end
end

:шоссе4
save шоссе4
btn шоссе4й, ok
instr трек4 = system_of_a_down_-_lonely_day
end

:шоссе4й
Use_машина_Выйти_hide = 1
Use_машина_Ехать дальше_hide = 1
inv- рюкзак
inv- бутылка
inv- пакет
inv+ Заднее сиденье
вдвижении = 1
if рюкзакубрал = 0 then pln 	Высыпаю содержимое рюкзака, прячу в него ломик и респиратор и засовываю под пассажирское сиденье.
if рюкзакубрал = 1 then pln 	Стягиваю респиратор и бросаю его и ломик под пассажирское сиденье.
pln
pln 	Посмотрев на кучу припасов на заднем сиденьи, я завожу двигатель и медленно выкатываюсь мимо магазина на площадку с колонками. В последний раз оглядев станцию, выезжаю на шоссе и вдавливаю педаль газа.
p 	Снова рычанье мотора, бескрайняя степь и сизые горы на горизонте.
if сиг > 0 and курение >= 4 then pln  Я достаю из бардачка сигарету и закуриваю. & сиг = сиг - 1 & курение = -1 & Use_бардачок_Взять сигарету_hide = 0;
голова = 5
proc события
end

:флешбэк3
cls
decor_back_color = 0xffffff
textcolor = 0x888888
pln 	"Ого! Неплохо," - я присвистнул, глядя вслед медсестре, с которой столкнулся, когда входил в палату. - "А Сун знает, что у тебя тут такие красотки водятся?"
pln 	"Пока нет, но если что, теперь я знаю кого нужно будет в этом винить."
pln 	Идеально белая палата освещалась солнечным светом. Небольшой телевизор, висевший напротив кровати, бормотал новости.
pln 	"Привет, пациент. Как самочувствие?"
pln 	"Как с сильного перепоя. Жажда мучает, голова раскалывается, но как пил, не помню." - голос тихий, рукопожатие слабое. - "Как тебя пропустили-то? Я же на карантине."
pln 	"У меня вроде как иммунитет к этой фигне. Еще с прошлого раза."
pln 	"А, это когда ты две недели лежал с температурой и все порывался завещание написать? 'Ким, кх-кх, по-моему, мне уже недолго осталось, кх-кх.' Конечно помню."
pln 	"Я действительно тогда чуть не умер. И я помню, что мы договаривались больше не поднимать эту тему."
pln 	"Да, точно. Все время забываю," - он начал кашлять и не мог остановиться, пока не выпил поданый мною стакан воды. - "Спасибо. Кашель никак не проходит. Кстати, ты купил себе палатку?"
pln 	"Хм, палатка, да... Просто никак не выкрою время, чтобы съездить купить."
pln 	"Макс, мы же давно уже запланировали."
pln 	"Конечно, я помню. Ты с Сун, я с какой-нибудь красоткой. Пляж, солнце, океан. Целая неделя на побережье. Все будет тип-топ."
pln 	В этот момент в палату вошла медсестра: "Простите, но пора проводить процедуры. Вам нужно уйти."
pln 	Я встал со стула и протянул руку Киму, которую он крепко сжал: "Макс, пообещай мне, что в этот раз, во что бы то ни стало, мы поедем."
pln 	"Обещаю, Ким. В этот раз ты увидишь океан," - я похлопал его по плечу. - "Выздоравливай."
pln 	В дверях я еще раз осмотрел медсестру, отвернувшуюся к столику с лекарствами, и показал Киму большой палец. Тот улыбнулся и сипло проговорил: "Проваливай."
pln 	На следующий день его не стало.
decorcol fade 0x01ffffff, 2000
pause 2000
anykey
decorcol fade 0xffffffff, 2000
былфлешбек = 2
курение = 5
pause 2000
goto шоссе5
end

:шоссе5
cls
invkill
номер_трека = 4
music_looped = 0
decor_back_color = 0xfff8dc
textcolor = 0x7f6500
pln 	Прошло уже несколько месяцев после смерти Кима. Но я все помню отчетливо. Каменные лица родителей, опломбированный гроб, люди с документами министерства здравохранения, дым над крематорием и металлическая урна, прижатая к груди рыдающей Сун. И я теперь знаю, что это было только начало.
pln 	
decorcol fade 0x01ffffff, 2000
fademusic громкостьвмашине, 2000
music #%трек#номер_трека$$.mp3, 2000
pause 2000
decorcol fade 0x01fff8dc
anykey
decorcol fade 0xfffff8dc, 2000
pause 2000
cls
pln 	Когда все началось, Болезни не придали значения. Привычная сезонная вспышка, легко прогнозируемая и контролируемая.
pln 	Все развивалось вполне обыденно. Власти, твердящие о контроле ситуации. СМИ, наперебой придумывающие названия. Ежечасные рекомендации избегать общественных мест. Герои детских передач, показывающие, как надевать маску. Реклама лекарств.
pln 	Когда количество умерших превысило миллион, началась паника. Люди скупали всю еду и воду, которые могли достать, баррикадировались в домах, мародерствовали, убивали за лекарства. Начальство Всемирной организации здравоохранения разводило руками на вопросы о вакцине. Непрекращающиеся беспорядки у штаб-квартиры ВОЗ в Женеве. Вымирающая Африка. Сотни тысяч беженцев, переносящих Болезнь все дальше. Потопленные гражданские суда в водах Японии, закрывшей границы. Переполненные церкви и мечети. Понтифик, чихающий посреди мессы. Плачущий экипаж космической станции, прощающийся с родными. Военные блокпосты вокруг горящих городов в зоне карантина. Мертвые солдаты на них. Обмен ядерными ударами между Индией и Пакистаном.
pln 	
pln 	Потребовалось всего лишь несколько месяцев, чтобы цивилизация перестала существовать.
decorcol fade 0x01fff8dc, 2000
pause 2000
anykey
decorcol fade 0xfffff8dc, 2000
pause 2000
cls
pln 	Но человечество не вымерло полностью. Кто-то был достаточно осторожен, чтобы не заразиться. Кому-то просто повезло. У пары сотен, как и у меня, был иммунитет. Я несколько раз видел движущиеся машины в пустых городах, отблески костров в ночном поле. Однажды даже двухмоторный самолет.
pln 
pln 	Горстка выживших на руинах могучей цивилизации. Смогут ли они восстановить былую мощь, воспользовавшись ставшими вдруг ничейными богатствами и знаниями? Смогут ли возродить человечество?
pln 
pln 	Может быть, почему бы и нет. Только мне все равно.
pln 
pln 
pln 	Я просто еду за рулем Транс-Эма семьдесят девятого года в сторону океана, чтобы выполнить обещание, данное умирающему другу.
decorcol fade 0x01fff8dc, 2000
pause 2000
anykey
decorcol fade 0xff000000, 2000
pause 3000
cls
textpane_top = 500
btn отавтора, От автора
btn сначала, Сначала
hide_btn_echo = 1
decorcol back 0x01000000
decorcol fade 0x01000000, 2000
pause 4000
goto конец
end

;---------------------------------------------------------------------------------------------------

:Use_машина_Oсмотреть
if таосмотр = 0 then pln 	Понтиак Транс-Эм 1979 года. Идеальное состояние. Минимум изменений - новый радиоприемник да обновленная краска золотисто-карамельного цвета - без дурацкого феникса на капоте.#/$ 	Этого монстра я увидел на подъездной дорожке ухоженного таунхауса в городке, название которого уже забыл. Увидел и вспомнил, что мечтал о таком все свое детство.#/$  	Ключи были спрятаны за солнцезащитным козырьком.
if таосмотр = 1 then pln 	Транс-Эм - это Понтиак Фаерберд с улучшенным управлением, более мощным двигателем, аэродинамическим обвесом и декалями.#/$ 	Модель Фаерберд появилась в 1967 году, как ответ Форду Мустанг и Шевроле Камаро, и насчитывает четыре поколения.#/$ 	Набор Транс-Эм начал продаваться в 1969 году и оставался топовой комплектацией на всем протяжении существования модели.
if таосмотр = 2 then pln 	В начале семидесятых продажи мускул-каров упали катастрофически и к середине десятилетия Понтиак Фаерберд оставался последним "настоящим" мускул-каром.#/$ 	Но и он начал поддаваться веянию нового времени: из-за ужесточающихся требований по выбросам, в 1976 году прекратился выпуск 455х двигателей объемом 7,5 литров, последних легендарных двигателей "мускул-эры".
if таосмотр = 3 then pln 	Но несмотря ни на что, 1978 год стал пиком продаж Фаербердов. Еще бы, ведь сам Берт Рейнольдс водил эксклюзивный черно-золотой Транс-Эм в хите "Полицейский и Бандит".#/$ 	После такого успеха, Фаерберд был окончательно признан лучшим автомобилем в своем классе.
if таосмотр = 4 then pln 	В 1979 году были проведены последние минорные обновления внешности Фаербердов второго поколения. Всего восемь тысяч Транс-Эмов из ста комплектовались двигателями Понтиак 400 с объемом 6,5 литров - больше эти двигатели не производились.#/$ 	В следующие, заключительные, два года второго поколения продажи Фаербердов упали на треть из-за неудачных топовых двигателей.
if таосмотр > 4 then pln 	Мой Транс-Эм как раз оснащен одним из последних 400х двигателей с мощностью в 220 лошадиных сил. Оригинальные, "сотовые", колесные диски. Крыша T-top со съемными прозрачными панелями над водителем и пассажиром. Бежевый кожаный салон и сиденья.#/$ 	Всего несколько месяцев назад он бы стоил под сотню тысяч долларов. Теперь - бесплатно.
proc события
таосмотр = таосмотр + 1
end

:Use_машина_Oстановиться
вдвижении = 0
нагревание = 0
Use_машина_Oстановиться_hide = 1
pln 	 Я плавно торможу, сворачиваю на обочину, подняв облако рыжей пыли, и глушу мотор.
Use_машина_Выйти_hide = 0
end

:Use_машина_Выйти
if патрубок = 2 and продукты = 1 and вода = 2 and сполок = 1 then pln 	Больше мне на этой заправке делать нечего. Пора ехать. & end
Use_машина_Выйти_hide = 1
Use_рюкзак_Освободить рюкзак_hide = 1
вмашине = 0
if inv_рюкзак > 0 then invkill & inv+ машина & inv+ рюкзак else invkill & inv+ машина
if естьшланг = 1 and шлангнапункте = 0 and машинанаазс = 2 then inv+ Шланг
if естькомпресс = 1 and компресснапункте = 0 and машинанаазс = 2 then inv+ Компрессор
if курение <= 4 and машинанаазс = 2 then pln 	Перед выходом предусмотрительно тушу сигарету в пепельнице. & курение = 5
pln 	Открываю дверь и выбираюсь из машины.
if былфлешбек = 1 then pln & goto удвери 
if былфлешбек = 2 and машинанаазс = 1 then inv+ азс & idisp_азс = "Навес" & goto удвери2
if былфлешбек = 2 and машинанаазс = 2 then inv+ азс & idisp_азс = "Пункт налива" & goto удвери3
/*Use_азс_Обойти здание с восточной стороны_hide = 1
Use_азс_Обойти здание с западной стороны_hide = 1
Use_азс_Идти к южной стороне здания_hide = 0
Use_азс_Идти к главному фасаду_hide = 1*/
end

:Use_машина_Внутрь
if inv_бутылка = 0 and былфлешбек = 1 then pln 	Я еще не взял воду.
if inv_бак = 1 then pln 	Нет, я должен посмотреть, что там блестит.
if inv_бутылка = 1 and виделбак = 0 and inv_бак = 0 then pln 	Когда я, потянув за ручку двери, оглядываю поверх крыши простирающуюся до горизонта степь, то вижу, как что-то блестит метрах в двадцати от дороги. Хм. & inv+ бак & idisp_бак = "Что-то блестящее"
if виделбак = 1 and былфлешбек = 1 then inv+ радио & inv+ бардачок & inv+ урна & fademusic громкостьвмашине, 500 & pln 	Распахнув дверь, забираюсь в обволакивающую духоту Транс-Эма, запускаю двигатель и выезжаю с пыльной обочины на черный асфальт. & pln 	Вдавливаю педаль газа, чтобы встречным потоком воздуха вытянуло весь жар из салона, и расслабленно откидываюсь на спинку сиденья. & Use_машина_Oстановиться_hide = 1 & Use_машина_Внутрь_hide = 1 & Use_машина_Идти к багажнику_hide = 1 & вдвижении = 1 & вмашине = 1
if виделбак = 1 and былфлешбек = 1 and Use_пакет_Оставить себе_hide <> 1 then флешбекбезтравки = 0 
if былфлешбек = 2 then inv+ радио & inv+ бардачок & inv+ урна & inv+ бутылка & inv- азс & fademusic громкостьвмашине, 500 & pln 	Распахнув дверь, забираюсь на водительское сиденье Транс-Эма. & Use_машина_Внутрь_hide = 1 & Use_машина_Идти к багажнику_hide = 1 & Use_машина_Выйти_hide = 0 & вмашине = 1
if былфлешбек = 2 and Use_пакет_Оставить себе_hide = 1 then inv+ пакет
if былфлешбек = 2 and вес = 2 then Use_рюкзак_Освободить рюкзак_hide = 0
if патрубок = 1 and машинанаазс = 1 then Use_машина_Переехать на пункт налива_hide = 0
inv- патрубок
if компресснапункте = 1 and машинанаазс = 2 then inv- Компрессор
if шлангнапункте = 1 and машинанаазс = 2 then inv- Шланг
if патрубок = 2 and продукты = 1 and вода = 2 and сполок = 1 then Use_машина_Ехать дальше_hide = 0
end

:Use_машина_Идти к багажнику
Use_машина_Идти к багажнику_hide = 1
Use_машина_Внутрь_hide = 1
fademusic громкостьвмашине/4, 500
inv_багажник = 1
idisp_багажник = "Багажник"
if былфлешбек = 1 then pln 	Ступая по горячему асфальту, подхожу к багажнику.
if былфлешбек = 2 then pln 	Огибаю машину и подхожу к багажнику.
proc события
Use_машина_Идти к водительской двери_hide = 0
end

:Use_машина_Идти к водительской двери
Use_машина_Идти к водительской двери_hide = 1
if былфлешбек = 1 then goto удвери
if былфлешбек = 2 then pln 	Огибаю машину и подхожу к водительской двери & fademusic громкостьвмашине/2, 500 & inv- багажник & proc события & Use_машина_Внутрь_hide = 0 & Use_машина_Идти к багажнику_hide = 0 & Use_машина_Идти к водительской двери_hide = 1
end

:Use_машина_Переехать на пункт налива
pln 	Завожу мотор и медленно выезжаю из-под навеса, поворачиваю к югу, объезжаю здание супермаркета с восточной стороны и, порыкивая двигателем, подкатываю по ярко освещенной площадке к пункту налива. Глушу мотор.
машинанаазс = 2
Use_машина_Переехать на пункт налива_hide = 1
end

:Use_машина_Ехать дальше
goto шоссе4
end

:Use_радио_Oсмотреть
pln 	Оказалось, что музыкальные вкусы бывшего хозяина меня вполне устраивают и теперь я с удовольствием слушаю его подборку.
p 	Бегущей строкой на экране показано название играющего трека:
textalign = 3
if номер_трека = 1 then pln "Pink Floyd - Hey You"
if номер_трека = 2 then pln "Jefferson Airplane - White Rabbit"
if номер_трека = 3 then pln "The Doors - The End"
textalign = 1
proc события
end

:Use_радио_Уменьшить громкость
if music_volume > 63.75 then music_volume = music_volume - 255/4 & pln 	Громкость: #(music_volume/255)*100$%;
_else music_volume = 0 & pln 	Громкость на нуле
громкостьвмашине = music_volume
proc события
end

:Use_радио_Увеличить громкость
if music_volume < 191.25 then music_volume = music_volume + 255/4 & pln 	Громкость: #(music_volume/255)*100$%;
_else music_volume = 255 & pln 	Громкость на максимуме
громкостьвмашине = music_volume
proc события
end 

:Use_радио_Выключить
if вдвижении = 1 then pln 	Я жму кнопку выключения на приемнике и остаюсь один на один с урчанием мотора и шумом ветра. else pln 	Я жму кнопку выключения на приемнике и остаюсь в тишине.
music stop, 500
Use_радио_Предыдущий трек_hide = 1
Use_радио_Следующий трек_hide = 1
Use_радио_Уменьшить громкость_hide = 1
Use_радио_Увеличить громкость_hide = 1
Use_радио_Выключить_hide = 1
Use_радио_Включить_hide = 0
proc события
end

:Use_радио_Включить
pln 	Я включаю приемник и на его экране загорается сообщение о чтении диска.
music #%трек#номер_трека$$.mp3, 500
Use_радио_Предыдущий трек_hide = 0
Use_радио_Следующий трек_hide = 0
Use_радио_Уменьшить громкость_hide = 0
Use_радио_Увеличить громкость_hide = 0
Use_радио_Выключить_hide = 0
Use_радио_Включить_hide = 1
proc события
end

:Use_бардачок_Oсмотреть
pln 	Неприметный прямоугольник на отделанной светлой кожей приборной панели. Внутри, среди проспектов и карт, лежит пачка сигарет.
Use_бардачок_Взять сигарету_hide = 0;
proc события
end

:Use_бардачок_Взять сигарету
if курение < 4 then pln 	Я еще не докурил предыдущую.
if сиг = 0 and курение >= 4 and былфлешбек <> 2 then pln 	Пачка пуста. Где же теперь найти сигареты посреди этой пустыни?
if сиг = 0 and курение >= 4 and былфлешбек = 2 and сполок = 0 then pln 	Пачка пуста. Нужно поискать сигареты в магазине.
if сиг = 0 and курение >= 4 and былфлешбек = 2 and сполок = 1 then pln 	Вытаскиваю из блока, найденного в магазине, новую пачку. & сиг = 20
if сиг > 0 and курение >= 4 and былфлешбек <> 2  then сиг = сиг - 1 & proc сколько_сигарет & pln 	Одной рукой придерживая руль, другой я достаю из пачки сигарету, жму на кнопку прикуривателя и, когда тот выщелкивает, подношу его к сигарете. Раскаленная спираль спаливает белый кончик и я делаю глубокий вдох. & pln 	Прежде, чем вернуть все обратно, я заглядываю в пачку - #%пачка$. & курение = -1
if сиг > 0 and курение >= 4 and былфлешбек = 2 and Use_рюкзак_Снять респиратор_hide = 1 then сиг = сиг - 1 & proc сколько_сигарет & pln 	Достаю из пачки сигарету, жму на кнопку прикуривателя и, когда тот выщелкивает, подношу его к сигарете. Раскаленная спираль спаливает белый кончик и я делаю глубокий вдох. & pln 	Прежде, чем вернуть все в бардачок, я заглядываю в пачку - #%пачка$. & курение = -1
if сиг > 0 and курение >= 4 and былфлешбек = 2 and Use_рюкзак_Снять респиратор_hide = 0 then сиг = сиг - 1 & proc сколько_сигарет & pln 	Стягиваю респиратор под подбородок, достаю из пачки сигарету, жму на кнопку прикуривателя и, когда тот выщелкивает, подношу его к сигарете. Раскаленная спираль спаливает белый кончик и я делаю глубокий вдох. & pln 	Прежде, чем вернуть все в бардачок, я заглядываю в пачку - #%пачка$. & курение = -1 & Use_рюкзак_Снять респиратор_hide = 1 & Use_рюкзак_Надеть респиратор_hide = 0
proc события
end

:Use_урна_Oсмотреть
pln 	Запечатанный матовый металлический сосуд, лежащий на пассажирском сиденьи.
pln 	На верхней части небольшая пластинка с фамилией, именем и датами.
if былфлешбек = 0 then Use_урна_Коснуться_hide = 0
proc события
end

:Use_урна_Коснуться
pln 	Не глядя, я в тысячный раз провожу пальцами по гладкой пластинке с символами.
pln 	Ким...
anykey
invkill
decorcol fade 0xffffffff, 2000
fademusic 0, 2000
;music stop, 2000
pause 2000
Use_урна_Коснуться_hide = 1
goto флешбэк1
end

:Use_багажник_Oсмотреть
if Use_багажник_Открыть_hide = 0 then pln 	Контур крышки небольшого багажника. На ее торце раньше, до покраски, были крупные буквы "TRANS AM".;
_else pln 	Багажник открыт, внутри - типичный для мускул-каров небольшой объем, в котором едва помещаются запаска, рюкзак и куб холодильника.
proc события
pln
end

:Use_багажник_Открыть
if inv_бутылка = 1 then pln 	Я уже взял воду, в багажнике мне больше ничего не нужно.
if inv_бутылка = 0 and былфлешбек = 1 then pln 	Стараясь меньше касаться горячего металла, я поднимаю крышку багажника. Там, в тесноте, ютятся запасное колесо, рюкзак и дорожный холодильник. &;
_Use_багажник_Открыть холодильник_hide = 0 &;
_Use_багажник_Открыть_hide = 1
if inv_рюкзак = 1 then pln 	Кроме рюкзака мне в багажнике больше ничего не нужно.
if inv_рюкзак = 0 and былфлешбек = 2 then pln 	Открываю крышку багажника - внутри все так же: запаска, холодильник и нужный мне рюкзак. &;
_Use_багажник_Взять рюкзак_hide = 0 &;
_Use_багажник_Открыть_hide = 1
proc события
pln
end

:Use_багажник_Открыть холодильник
pln 	Внутри стоят полулитровые бутылки питьевой воды. Главная ценность в такую жару.
Use_багажник_Открыть холодильник_hide = 1
Use_багажник_Взять воду_hide = 0
proc события
pln
end

:Use_багажник_Взять воду
inv+ бутылка
idisp_бутылка = "Пара бутылок воды"
pln 	Я вынимаю мгновенно запотевшую бутылку, открываю и с наслаждением пью ледяную воду, потом беру еще одну прозапас, плотно закрываю холодильник и захлопываю крышку багажника.
Use_багажник_Открыть холодильник_hide = 1
Use_багажник_Взять воду_hide = 1
Use_багажник_Открыть_hide = 0
proc события
pln
end

:Use_багажник_Взять рюкзак
inv+ рюкзак
idisp_рюкзак = "Набор мародера"
pln 	Я вытаскиваю рюкзак, набрасываю его на одно плечо и закрываю крышку багажника.
Use_багажник_Взять рюкзак_hide = 1
Use_багажник_Открыть_hide = 0
proc события
pln
end

:Use_бутылка_Осмотреть
pln 	Бутилированная артезианская вода, стоимость которой совсем недавно приближалась к стоимости неплохого вина того же объема.
proc события
end

:Use_бак_Проверить
Use_бак_Проверить_hide = 1
inv- машина
if inv_багажник = 1 then inv- багажник
pln 	Обогнув машину, пересекаю обочину и иду среди жестких, колючих кустов. Слабый ветер теребит полы растегнутой рубашки и немного облегчает пребывание под безжалостным солнцем. Попадающиеся под ногами камни плоские и ломкие.
pln
fademusic громкостьвмашине/50, 500
виделбак = 1
idisp_бак = "Бак"
pln 	Наконец, я добираюсь до цели своей вылазки и вижу помятый мотоциклетный бак. Когда-то хромированный, теперь с пятнами ржавчины, он лежит на ровной поверхности большого выбеленного валуна, словно на постаменте. Кто-то установил его сюда с определенной целью.
proc события
Use_бак_Осмотреть_hide = 0
Use_бак_Пнуть_hide = 0
end

:Use_бак_Осмотреть
if inv_пакет = 1 then pln 	Больше внутри бака ничего нет.
if inv_пакет = 0 then pln 	Я подхожу, встряхиваю бак, убедившись, что внутри что-то есть, с усилием откручиваю крышку и вытягиваю из бака продолговатый пакет, плотно набитый сухими измельченными листьями. Мэри Джейн. &;
_Use_бак_Пнуть_hide = 1 &;
_inv+ пакет &;
_idisp_пакет = "Пакет с травкой"
proc события
end

:Use_бак_Пнуть
pln 	От удара бак подскакивает и скатывается с валуна. По звуку похоже, что внутри него что-то есть.
Use_бак_Пнуть_hide = 1
proc события
end

:Use_пакет_Осмотреть
if вмашине = 0 then pln 	На пакет приклеена записка: "Койот, ты всегда был придурком и не следил за своим конем, вот он тебя и подвел. Держи немного дури, чтобы не скучать на том свете. Банда."
if вмашине = 1 then pln 	Пакет я бросил на заднее сиденье до тех пор, пока не удастся запастись сигаретной бумагой. Такого количества травки мне хватит надолго.
if былфлешбек = 1 and вмашине = 1 then pln & pln 	И вдруг я с особенной остротой чувствую свое одиночество... &;
_anykey &;
_invkill &;
_decorcol fade 0xffffffff, 2000 &;
_fademusic 0, 2000 &;
;_music stop, 2000 &;
_pause 2000 &;
_goto флешбэк2
proc события
end

:Use_пакет_Оставить себе
inv- бак
pln 	Вряд ли тот, кому этот пакет предназначался, когда-нибудь придет за ним. Поэтому я решаю оставить его себе.
pln 	Закрываю крышку, ставлю бак на валун, и, попивая уже теплую воду, возвращаюсь назад, к плывущей в мареве машине.
anykey
pln
inv+ машина
Use_пакет_Оставить себе_hide = 1
Use_пакет_Сунуть обратно_hide = 1
goto удвери
end

:Use_пакет_Сунуть обратно
inv- бак
inv- пакет
pln 	Чувствуя себя неловко, словно вскрыл чью-то могилу, я решаю вернуть все как было.
pln 	Засовываю пакет обратно, закрываю крышку, ставлю бак на валун, и, попивая уже теплую воду, возвращаюсь назад, к плывущей в мареве машине.
anykey
pln
inv+ машина
goto удвери
end

:Use_азс_Осмотреться
if гденаазс < 8 then pln 	Оглядываюсь. & proc осмотразс
if гденаазс > 7 and фонарьвкл = 1 then proc осмотразс & decor_shop_text = "	Обвожу вокруг лучом фонаря.#/$#/$" + описание
if гденаазс > 7 and фонарьвкл = 0 then proc осмотразс & decor_shop_text = "	Вглядываюсь в окружающую меня темноту.#/$#/$" + описание
proc события
end

:Use_азс_Прочесть записку
textcolor = 0x666666
textfont = "segoe.fnt"
pln Магазин и заправочная станция закрыты на время чрезвычайного положения. Приносим свои извинения.
textalign = 2
pln Администрация
textalign = 1
textcolor = 0x7f6500
textfont = "_sysfont"
proc события
end

:Use_азс_Осмотреть насос
pln 	 Для меня вещь бесполезная - даже если бы и было электричество, насос работает только на прием топлива, а пробраться через него, систему фильтров, контрольных датчиков и противопожарных задвижек к резервуарам вряд ли возможно.
proc события
end

:Use_азс_Осмотреть ящик
pln 	Обычно, уровень топлива контролируется электроникой, но возможность ручной проверки всегда должна быть, для этого и служит замерный патрубок, обеспечивающий доступ прямо к резервуарам. Иного пути добыть горючее у меня нет.
pln 	Чтобы ограничить доступ посторонних к патрубку, тот выведен в металлический ящик, в данный момент запертый на висячий замок.
proc события
end

:Use_азс_Идти под навес
idisp_азс = "Навес"
гденаазс = 1
if машинанаазс = 1 then pln 	Я подхожу к отдыхающему в тени Транс-Эму. & inv+ машина & fademusic громкостьвмашине/2, 500 & Use_машина_Внутрь_hide = 0 & Use_машина_Идти к багажнику_hide = 0 & Use_машина_Идти к водительской двери_hide = 1 else pln 	Я прохожу к заправочным колонкам.
proc осмотразс
if машинанаазс = 1 and вес = 2 then pln & pln 	Надо бы разгрузить рюкзак, раз я у машины. Гулять с тяжестью по такой жаре не очень приятно. Высыплю все пока на заднее сиденье.
Use_азс_Влезть в окно_hide = 1
Use_азс_Прочесть записку_hide = 1
Use_азс_Идти под навес_hide = 1
Use_азс_Идти к главному фасаду_hide = 0
Use_азс_Обойти здание с восточной стороны_hide = 0
Use_азс_Обойти здание с западной стороны_hide = 0
Use_азс_Идти к южной стороне здания_hide = 1
Use_азс_Идти к пункту налива_hide = 1
end

:Use_азс_Идти к главному фасаду
idisp_азс = "Главный фасад"
гденаазс = 2
if машинанаазс = 1 then fademusic громкостьвмашине/6, 500
if машинанаазс = 2 then fademusic 0, 500
if inv_машина = 1 then inv- машина
if inv_багажник = 1 then inv- багажник
pln 	Я неспеша иду к главному фасаду магазина.
proc осмотразс
Use_азс_Влезть в окно_hide = 1
Use_азс_Прочесть записку_hide = 0
Use_азс_Идти под навес_hide = 0
Use_азс_Идти к главному фасаду_hide = 1
Use_азс_Обойти здание с восточной стороны_hide = 0
Use_азс_Обойти здание с западной стороны_hide = 0
Use_азс_Идти к южной стороне здания_hide = 1
Use_азс_Идти к пункту налива_hide = 1
end

:Use_азс_Обойти здание с восточной стороны
idisp_азс = "Восточная стена"
гденаазс = 3
fademusic громкостьвмашине/20, 500
if inv_машина = 1 then inv- машина
if inv_багажник = 1 then inv- багажник
pln 	Я захожу в тень с восточной стороны здания.
proc осмотразс
Use_азс_Прочесть записку_hide = 1
Use_азс_Идти под навес_hide = 0
Use_азс_Идти к главному фасаду_hide = 0
Use_азс_Обойти здание с восточной стороны_hide = 1
Use_азс_Обойти здание с западной стороны_hide = 1
Use_азс_Идти к южной стороне здания_hide = 0
Use_азс_Идти к пункту налива_hide = 1
Use_азс_Идти на склад_hide = 1
end

:Use_азс_Обойти здание с западной стороны
idisp_азс = "Западная стена"
гденаазс = 4
fademusic громкостьвмашине/20, 500
if inv_машина = 1 then inv- машина
if inv_багажник = 1 then inv- багажник
pln 	Я выхожу к освещенной стороне здания.
proc осмотразс
if окноразбито = 1 then Use_азс_Влезть в окно_hide = 0
Use_азс_Прочесть записку_hide = 1
Use_азс_Идти под навес_hide = 0
Use_азс_Идти к главному фасаду_hide = 0
Use_азс_Обойти здание с восточной стороны_hide = 1
Use_азс_Обойти здание с западной стороны_hide = 1
Use_азс_Идти к южной стороне здания_hide = 0
Use_азс_Идти к пункту налива_hide = 1
Use_азс_Идти на склад_hide = 1
end

:Use_азс_Идти к южной стороне здания
idisp_азс = "Южная стена"
гденаазс = 5
if машинанаазс = 1 then fademusic 0, 500
if машинанаазс = 2 then fademusic громкостьвмашине/6, 500 & inv- машина
inv- патрубок
inv- Шланг
inv- Компрессор
pln 	Я прохожу к южной стороне здания.
proc осмотразс
Use_азс_Влезть в окно_hide = 1
Use_азс_Осмотреть насос_hide = 1
Use_азс_Осмотреть ящик_hide = 1
Use_азс_Идти под навес_hide = 1
Use_азс_Идти к главному фасаду_hide = 1
Use_азс_Обойти здание с восточной стороны_hide = 0
Use_азс_Обойти здание с западной стороны_hide = 0
Use_азс_Идти к южной стороне здания_hide = 1
Use_азс_Идти к пункту налива_hide = 0
Use_азс_Идти на склад_hide = 0
end

:Use_азс_Идти к пункту налива
idisp_азс = "Пункт налива"
гденаазс = 6
if машинанаазс = 1 then pln 	Я выхожу из-под козырька и иду к оборудованию на пункте налива.
if машинанаазс = 2 then pln 	Я выхожу из-под козырька и иду к Транс-Эму, стоящему на пункте налива. & inv+ машина & fademusic громкостьвмашине/2, 500
proc осмотразс
Use_азс_Осмотреть насос_hide = 0
if ящикоткрыт = 0 then Use_азс_Осмотреть ящик_hide = 0
if ящикоткрыт = 1 then inv+ патрубок
if шлангнапункте = 1 and патрубок <> 2 then inv+ Шланг
if компресснапункте = 1 and патрубок <> 2 then inv+ Компрессор
Use_азс_Обойти здание с восточной стороны_hide = 1
Use_азс_Обойти здание с западной стороны_hide = 1
Use_азс_Идти к южной стороне здания_hide = 0
Use_азс_Идти к пункту налива_hide = 1
Use_азс_Идти на склад_hide = 1
if естьшланг = 1 and естькомпресс = 0 and шлангнапункте = 0 then pln 	Чтобы не таскаться повсюду со шлангом, оставляю его в тени у насоса. & шлангнапункте = 1
if естьшланг = 1 and естькомпресс = 1 and компресснапункте = 1 and шлангнапункте = 0 then pln 	Чтобы не таскаться повсюду со шлангом, оставляю его рядом с компрессором в тени у насоса. & шлангнапункте = 1
if естьшланг = 0 and естькомпресс = 1 and компресснапункте = 0 then pln 	Чтобы не таскаться повсюду с компрессором, ставлю его в тени у насоса. & компресснапункте = 1
if естьшланг = 1 and естькомпресс = 1 and компресснапункте = 0 and шлангнапункте = 1 then pln 	Чтобы не таскаться повсюду с компрессором, ставлю его рядом со шлангом в тени у насоса. & компресснапункте = 1
if естьшланг = 1 and естькомпресс = 1 and компресснапункте = 0 and шлангнапункте = 0 then pln 	Чтобы не таскать повсюду компрессор и шланг оставляю их в тени у насоса. & компресснапункте = 1 & шлангнапункте = 1
if машинанаазс = 2 and вес = 2 then pln & pln 	Надо разгрузить рюкзак. Высыплю все пока на заднее сиденье.
if продукты = 1 and вода = 2 and сполок = 1 and патрубок = 2 then pln 	Я сделал все, что хотел, теперь можно ехать. & end
if патрубок = 2 then if продукты <> 1 or вода <> 2 or сполок <> 1 then pln 	Нужно проверить, что еще можно собрать в магазине, и можно ехать. & end
if продукты <> 1 and вода <> 2 and сполок <> 1 and патрубок = 2 then pln 	Нужно проверить, что можно собрать в магазине, и можно ехать. & end
end

:Use_азс_Влезть в окно
idisp_азс = "Операторная"
гденаазс = 7
fademusic 0, 500
inv+ Стол
inv+ Зеркало
inv+ Календарь
inv+ План эвакуации
if виделтетрадь = 1 then inv+ Тетрадь
if курение > 4 then pln 	Я осторожно, стараясь не порезаться, пролезаю в окно и, отодвинув в сторону жалюзи, спрыгиваю на усыпанный осколками пол.
if курение <= 4 then pln 	Предварительно затушив сигарету, я медленно, стараясь не порезаться, пролезаю в окно и, отодвинув в сторону жалюзи, спрыгиваю на усыпанный осколками пол. & курение = 5
decorcol back 0xcdcabc, 200
textcolor = 0x554f36
proc осмотразс
Use_азс_Пройти в магазин_hide = 0
Use_азс_Влезть в окно_hide = 1
Use_азс_Вылезти через окно_hide = 0
Use_азс_Идти под навес_hide = 1
Use_азс_Идти к главному фасаду_hide = 1
Use_азс_Идти к южной стороне здания_hide = 1
end

:Use_азс_Вылезти через окно
idisp_азс = "Западная стена"
гденаазс = 4
fademusic громкостьвмашине/20, 500
inv- Стол
inv- Зеркало
inv- Календарь
inv- Тетрадь
inv- План эвакуации
pln 	Вскарабкиваюсь на подоконник и спрыгиваю с другой стороны на усыпанный осколками асфальт.
decorcol back 0xfff8dc, 200
textcolor = 0x7f6500
if фонарьвкл = 1 then pln 	Чтобы не садить батареи, выключаю фонарь. & Use_рюкзак_Включить фонарь_hide = 0 & Use_рюкзак_Выключить фонарь_hide = 1 & фонарьвкл = 0
proc осмотразс
Use_азс_Пройти в магазин_hide = 1
Use_азс_Влезть в окно_hide = 0
Use_азс_Вылезти через окно_hide = 1
Use_азс_Идти под навес_hide = 0
Use_азс_Идти к главному фасаду_hide = 0
Use_азс_Идти к южной стороне здания_hide = 0
end

:Use_азс_Пройти в магазин
if Use_рюкзак_Надеть респиратор_hide = 0 and знаниеозапахе = 1 then pln 	Помня о зловонии в магазине, предусмотрительно натягиваю респиратор и уже потом открываю дверь и шагаю в темноту. & Use_рюкзак_Надеть респиратор_hide = 1 & Use_рюкзак_Снять респиратор_hide = 0 & гденаазс = 12 & anykey & idisp_азс = "У двери в служебное помещение" & goto входвмагазин
if Use_рюкзак_Надеть респиратор_hide = 0 and знаниеозапахе = 0 then pln 	Открываю дверь и тут же отшатываюсь от хлынувшего из темноты смрада. Нет, дышать я этим не собираюсь. & знаниеозапахе = 1
if Use_рюкзак_Надеть респиратор_hide = 1 then pln 	Поправив респиратор на лице, шагаю в темноту. & знаниеозапахе = 1 & гденаазс = 12 & anykey & idisp_азс = "У двери в служебное помещение" & goto входвмагазин
end

:Use_азс_Войти в дверь
cls
hide_inv_echo = 0
decor_shop_hide = 1
decor_spot_hide = 1
idisp_азс = "Операторная"
гденаазс = 7
inv- Холодильники
inv+ Стол
inv+ Зеркало
inv+ Календарь
inv+ План эвакуации
if виделтетрадь = 1 then inv+ Тетрадь
pln 	Захожу в дверь и плотно закрываю ее за собой.
if фонарьвкл = 1 then pln 	В комнате достаточно светло, поэтому выключаю фонарь. & Use_рюкзак_Включить фонарь_hide = 0 & Use_рюкзак_Выключить фонарь_hide = 1 & фонарьвкл = 0
decorcol back 0xcdcabc, 200
textcolor = 0x554f36
proc осмотразс
Use_азс_Пройти в магазин_hide = 0
if окноразбито = 1 then Use_азс_Вылезти через окно_hide = 0
Use_азс_Идти в темноту_hide = 1
Use_азс_Войти в дверь_hide = 1
Use_азс_Идти вдоль конторки_hide = 1
Use_азс_Идти мимо холодильников_hide = 1
end

:Use_азс_Идти на склад
if складоткрыт = 0 then pln 	Я подхожу к красной двери и дергаю за ручку. Заперто.
if Use_рюкзак_Надеть респиратор_hide = 0 and знаниеозапахе = 1 and складоткрыт = 1 then pln 	Помня о зловонии в магазине, предусмотрительно натягиваю респиратор и уже потом открываю дверь и шагаю в темноту. & Use_рюкзак_Надеть респиратор_hide = 1 & Use_рюкзак_Снять респиратор_hide = 0 & гденаазс = 8 & anykey & idisp_азс = "На складе" & goto входвмагазин
if Use_рюкзак_Надеть респиратор_hide = 0 and знаниеозапахе = 0 and складоткрыт = 1 then pln 	Открываю дверь и тут же отшатываюсь от хлынувшего из темноты смрада. Нет, дышать я этим не собираюсь. & знаниеозапахе = 1
if Use_рюкзак_Надеть респиратор_hide = 1 and складоткрыт = 1 then pln 	Поправив респиратор на лице, шагаю в темноту. & знаниеозапахе = 1 & гденаазс = 8 & anykey & idisp_азс = "На складе" & goto входвмагазин
end

:Use_азс_Выйти на улицу
cls
hide_inv_echo = 0
decor_shop_hide = 1
decor_spot_hide = 1
idisp_азс = "Южная стена"
if машинанаазс = 2 then fademusic громкостьвмашине/6, 500 & inv- машина
гденаазс = 5
if естьшланг = 0 then inv- Шланг
decorcol back 0xfff8dc, 200
textcolor = 0x7f6500
if складоткрыт = 1 then pln 	Выхожу через красную дверь и плотно закрываю ее за собой.
if складоткрыт = 0 then pln 	Отпираю изнутри замок красной двери, выхожу и плотно закрываю ее за собой. & складоткрыт = 1
if фонарьвкл = 1 then pln 	Выключаю фонарь. & Use_рюкзак_Включить фонарь_hide = 0 & Use_рюкзак_Выключить фонарь_hide = 1 & фонарьвкл = 0
proc осмотразс
Use_азс_Выйти на улицу_hide = 1
Use_азс_Обойти здание с восточной стороны_hide = 0
Use_азс_Обойти здание с западной стороны_hide = 0
Use_азс_Идти к пункту налива_hide = 0
Use_азс_Идти на склад_hide = 0
Use_азс_Пройти через дверь в магазин_hide = 1
Use_азс_Идти в темноту_hide = 1
end

:Use_азс_Пройти через дверь в магазин
гденаазс = 10
idisp_азс = "У двери на склад"
inv+ Стеллажи с инструментами
if естьшланг = 0 then inv- Шланг
proc осмотразс
decor_shop_text = "	Осторожно открываю дверь и, бегло осветив обстановку, захожу в помещение магазина.#/$#/$" + описание
Use_азс_Идти мимо холодильников_hide = 0
Use_азс_Идти мимо стеллажей_hide = 0
Use_азс_Пройти на склад_hide = 0
Use_азс_Пройти через дверь в магазин_hide = 1
Use_азс_Выйти на улицу_hide = 1
end

:Use_азс_Пройти на склад
гденаазс = 8
idisp_азс = "На складе"
inv- Стеллажи с инструментами
if естьшланг = 0 then inv+ Шланг & idisp_Шланг = "Шланг на стене"
proc осмотразс
decor_shop_text = "	Открываю дверь и, поморщившись от запаха, прохожу на склад.#/$#/$" + описание
Use_азс_Идти мимо холодильников_hide = 1
Use_азс_Идти мимо стеллажей_hide = 1
Use_азс_Пройти на склад_hide = 1
Use_азс_Пройти через дверь в магазин_hide = 0
Use_азс_Выйти на улицу_hide = 0
end

:Use_азс_Идти в темноту
proc Use_рюкзак_Включить фонарь
decor_shop_text = "	''It is pitch black. You are likely to be eaten by a grue,'' - декламирую я и включаю фонарь.#/$#/$" + описание
end

:Use_азс_Идти мимо холодильников
if гденаазс = 10 then гденаазс = 12 & idisp_азс = "У двери в служебное помещение" & Use_азс_Войти в дверь_hide = 0 & inv+ Холодильники & Use_азс_Идти вдоль конторки_hide = 0 & Use_азс_Пройти на склад_hide = 1 & Use_азс_Идти мимо стеллажей_hide = 1 & inv- Стеллажи с инструментами & proc осмотразс & decor_shop_text = "	Пробираюсь мимо холодильников с напитками.#/$#/$" + описание & end
if гденаазс = 12 then гденаазс = 10 & idisp_азс = "У двери на склад" & Use_азс_Войти в дверь_hide = 1 & inv- Холодильники & Use_азс_Идти вдоль конторки_hide = 1 & Use_азс_Пройти на склад_hide = 0 & Use_азс_Идти мимо стеллажей_hide = 0 & inv+ Стеллажи с инструментами & proc осмотразс & decor_shop_text = "	Пробираюсь мимо холодильников с напитками к двери на склад.#/$#/$" + описание & end

:Use_азс_Идти вдоль конторки
if гденаазс = 11 then гденаазс = 12 & idisp_азс = "У двери в служебное помещение" & Use_азс_Идти мимо холодильников_hide = 0 & Use_азс_Войти в дверь_hide = 0 & inv+ Холодильники & Use_азс_Идти вдоль витрины_hide = 1 & inv- Полки & inv- Газеты & proc осмотразс & decor_shop_text = "	Ведя одной рукой по конторке, прохожу мимо стойки с газетами к двери с табличкой.#/$#/$" + описание & end
if гденаазс = 12 then гденаазс = 11 & idisp_азс = "У главного входа" & Use_азс_Идти мимо холодильников_hide = 1 & Use_азс_Войти в дверь_hide = 1 & inv- Холодильники & Use_азс_Идти вдоль витрины_hide = 0 & inv+ Полки & inv+ Газеты & proc осмотразс & decor_shop_text = "	Касаясь левой рукой конторки, прохожу к месту продавца и главному входу.#/$#/$" + описание & end

:Use_азс_Идти вдоль витрины
if гденаазс = 11 then гденаазс = 9 & idisp_азс = "У стеллажей с продуктами" & Use_азс_Идти мимо стеллажей_hide = 0 & inv+ Продуктовые стеллажи & Use_азс_Идти вдоль конторки_hide = 1 & inv- Полки & inv- Газеты & proc осмотразс & decor_shop_text = "	Иду мимо витрины к высоким стеллажам вдоль восточной стены.#/$#/$" + описание & end
if гденаазс = 9 then гденаазс = 11 & idisp_азс = "У главного входа" & Use_азс_Идти мимо стеллажей_hide = 1 & inv- Продуктовые стеллажи & Use_азс_Идти вдоль конторки_hide = 0 & inv+ Полки & inv+ Газеты & proc осмотразс & decor_shop_text = "	Иду мимо витрины к главному входу и конторке.#/$#/$" + описание & end

:Use_азс_Идти мимо стеллажей
if гденаазс = 10 then гденаазс = 9 & idisp_азс = "У стеллажей с продуктами" & inv+ Продуктовые стеллажи & Use_азс_Идти вдоль витрины_hide = 0 & Use_азс_Идти мимо холодильников_hide = 1 & Use_азс_Пройти на склад_hide = 1 & inv- Стеллажи с инструментами & proc осмотразс & decor_shop_text = "	Прохожу вдоль стеллажей к витрине.#/$#/$" + описание & end
if гденаазс = 9 then гденаазс = 10 & idisp_азс = "У двери на склад" & inv- Продуктовые стеллажи & Use_азс_Идти вдоль витрины_hide = 1 & Use_азс_Идти мимо холодильников_hide = 0 & Use_азс_Пройти на склад_hide = 0 & inv+ Стеллажи с инструментами & proc осмотразс & decor_shop_text = "	Иду, освещая товары на стеллажах, и подхожу к двери на склад. Здесь запах сильнее.#/$#/$" + описание & end

:Use_рюкзак_Осмотреть
if гденаазс < 8 and рюкзакубрал = 1 then pln 	Фонарь, ломик и респиратор. & end
if гденаазс > 7 and рюкзакубрал = 1 then decor_shop_text = "Фонарь, ломик и респиратор." & end
if гденаазс < 8 and знаниеозапахе = 0 then pln 	Фонарь, ломик и вместительный рюкзак - минимальный набор необходимых вещей. Нелишним также может оказаться и респиратор.
if гденаазс < 8 and знаниеозапахе = 1 and вес = 0 then pln 	Фонарь, ломик, вместительный рюкзак и все-таки пригодившийся респиратор.
if гденаазс < 8 and знаниеозапахе = 1 and вес = 1 then pln 	Фонарь, ломик, все-таки пригодившийся респиратор и забитый наполовину хабаром рюкзак.
if гденаазс < 8 and знаниеозапахе = 1 and вес = 2 then pln 	Фонарь, ломик, все-таки пригодившийся респиратор и доверху забитый хабаром рюкзак.
if гденаазс > 7 and вес = 0 then decor_shop_text = "Фонарь, ломик, вместительный рюкзак и все-таки пригодившийся респиратор."
if гденаазс > 7 and вес = 1 then decor_shop_text = "Фонарь, ломик, все-таки пригодившийся респиратор и забитый наполовину хабаром рюкзак."
if гденаазс > 7 and вес = 2 then decor_shop_text = "Фонарь, ломик, все-таки пригодившийся респиратор и доверху забитый хабаром рюкзак."
proc события
end

:Use_рюкзак_Включить фонарь
if гденаазс < 7 then pln 	Щелкаю переключателем, но света фонаря почти не видно - на улице слишком ярко. Чтобы поберечь батареи, выключаю.
if гденаазс = 7 then pln 	Щелкаю переключателем, но в комнате достаточно светло и без фонаря. Чтобы поберечь батареи, выключаю.
if гденаазс > 7 then Use_рюкзак_Включить фонарь_hide = 1 & Use_рюкзак_Выключить фонарь_hide = 0 & фонарьвкл = 1 & decor_shop_x = 200 & decor_shop_y = 210 & decor_shop_width = 400 & decor_shop_align = 3 & decor_spot_hide = 0 & decorcol shop 0xe6ecff, 100 & decor_shop_text = описание & былсветвмагазине = 1 & Use_азс_Идти в темноту_hide = 1
if гденаазс = 8 and естьшланг = 0 then inv+ Шланг & idisp_Шланг = "Шланг на стене" & Use_азс_Пройти через дверь в магазин_hide = 0
if гденаазс = 8 then Use_азс_Пройти через дверь в магазин_hide = 0
if гденаазс = 9 then Use_азс_Идти вдоль витрины_hide = 0 & Use_азс_Идти мимо стеллажей_hide = 0 & inv+ Продуктовые стеллажи
if гденаазс = 10 then Use_азс_Идти мимо холодильников_hide = 0 & Use_азс_Идти мимо стеллажей_hide = 0 & inv+ Стеллажи с инструментами
if гденаазс = 11 then Use_азс_Идти вдоль витрины_hide = 0 & Use_азс_Идти вдоль конторки_hide = 0 & inv+ Полки & inv+ Газеты
if гденаазс = 12 then Use_азс_Идти мимо холодильников_hide = 0 & Use_азс_Идти вдоль конторки_hide = 0 & inv+ Холодильники
proc события
end

:Use_рюкзак_Выключить фонарь
decorcol shop 0x222222, 100
pause 100
decor_shop_text = описание
decor_shop_x = 30
decor_shop_y = 55
decor_shop_width = 740
decor_shop_align = 1
decor_spot_hide = 1
Use_рюкзак_Включить фонарь_hide = 0
Use_рюкзак_Выключить фонарь_hide = 1
фонарьвкл = 0
if гденаазс > 7 then Use_азс_Идти в темноту_hide = 0 & decor_shop_text = описание
if гденаазс = 8 and естьшланг = 0 then inv- Шланг & Use_азс_Пройти через дверь в магазин_hide = 1
if гденаазс = 8 and естьшланг = 1 then Use_азс_Пройти через дверь в магазин_hide = 1
if гденаазс = 9 then Use_азс_Идти вдоль витрины_hide = 1 & Use_азс_Идти мимо стеллажей_hide = 1 & inv- Продуктовые стеллажи
if гденаазс = 10 then Use_азс_Идти мимо холодильников_hide = 1 & Use_азс_Идти мимо стеллажей_hide = 1 & inv- Стеллажи с инструментами
if гденаазс = 11 then Use_азс_Идти вдоль витрины_hide = 1 & Use_азс_Идти вдоль конторки_hide = 1 & inv- Полки & inv- Газеты & inv- Лотереи
if гденаазс = 12 then Use_азс_Идти мимо холодильников_hide = 1 & Use_азс_Идти вдоль конторки_hide = 1 & inv- Холодильники
proc события
end

:Use_рюкзак_Надеть респиратор
if курение <= 4 and вмашине = 0 then pln 	Прежде, чем надеть респиратор, тушу сигарету о подошву ботинка. & курение = 5
if курение <= 4 and вмашине = 1 then pln 	Прежде, чем надеть респиратор, тушу сигарету в пепельнице. & курение = 5
if курение > 4 then pln 	Надеваю респиратор и фиксирую металлическим зажимом на переносице.
Use_рюкзак_Надеть респиратор_hide = 1
Use_рюкзак_Снять респиратор_hide = 0
proc события
end

:Use_рюкзак_Снять респиратор
if гденаазс < 8 then pln 	Стягиваю респиратор под подбородок. & Use_рюкзак_Надеть респиратор_hide = 0 & Use_рюкзак_Снять респиратор_hide = 1
if гденаазс > 7 then decor_shop_text = описание + "#/$#/$Запах слишком сильный - лучше остаться в респираторе."
proc события
end

:Use_рюкзак_Использовать ломик
if вмашине = 1 then pln 	Решаю, что будет неразумно размахивать ломиком в машине. & end
if гденаазс = 1 and машинанаазс = 1  then pln 	Я кручу ломик в руке и едва не задеваю крыло Транс-Эма.
if гденаазс = 1 and машинанаазс = 2  then pln 	Я эффектно прокручиваю ломик пальцами.
if гденаазс = 2 then pln 	Я поддеваю ломиком нижний край одной из секций и, работая им как рычагом, пытаюсь поднять ставни. Но, естественно, ничего не выходит.
if гденаазс = 3 then pln 	Я подбрасываю ломик в воздух и с легкостью ловлю его.
if гденаазс = 4 and окноразбито = 1 then pln 	Я провожу ломиком по оконной раме, чтобы очистить ее от оставшихся осколков.
if гденаазс = 4 and окноразбито = 0 then pln 	Отвернувшись, чтобы осколки не попали в лицо, я ударяю в окно ломиком. Звон разбивающегося стекла разносится по всей округе. & окноразбито = 1 & Use_азс_Влезть в окно_hide = 0
if гденаазс = 5 and складоткрыт = 1 then pln 	C воротами мне не справиться, а дверь я уже открыл.
if гденаазс = 5 and складоткрыт = 0 then pln 	Осмотрев ворота и решив, что они мне не по зубам, я подхожу к красной двери. Сильным ударом вонзаю конец ломика в щель между косяком и дверью и несколькими толчками выламываю хлипкий замок. & складоткрыт = 1 & Use_азс_Идти на склад_hide = 0
if гденаазс = 6 and ящикоткрыт = 1 then pln 	Замок на дверце я уже сломал, а крышку с патрубка при помощи ломика не снять.
if гденаазс = 6 and ящикоткрыт = 0 then pln 	Я просовываю один конец ломика в дужку замка, упираю его и резко дергаю за другой. Дужка остается в петлях, а сам замок падает в пыль. & pln 	Освобождаю петли и открываю дверцу ящика. Это уже интересней. Судя по легко вращающейся крышке патрубка и цифрам на ее краях, она еще и исполняет роль кодового замка. Грубой силой здесь не разберешься, нужно искать комбинацию. & ящикоткрыт = 1 & inv+ патрубок & idisp_патрубок = "Крышка патрубка" & Use_азс_Осмотреть ящик_hide = 1
if гденаазс = 6 and машинанаазс = 2  then pln 	Я кручу ломик в руке и едва не задеваю крыло Транс-Эма.
if гденаазс = 7 and окноразбито = 1 then pln 	Я провожу ломиком по оконной раме, чтобы очистить ее от оставшихся осколков.
if гденаазс = 7 and окноразбито = 0 then pln 	Отвернувшись, чтобы осколки не попали в лицо, я ударяю в окно ломиком. Стекло разбивается с оглушительным для замкнутого помещения звоном. & окноразбито = 1 & Use_азс_Вылезти через окно_hide = 0
if гденаазс > 7 then decor_shop_text = описание + "#/$#/$Подумав, я решаю, что не буду размахивать ломиком в темноте."
end

:Use_рюкзак_Освободить рюкзак
if вода < 2 or сполок = 0 or продукты = 0 then pln 	Высыпаю все содержимое рюкзака на заднее сиденье. Разбирать буду позже.
if вода = 2 and сполок = 1 and продукты = 1 then pln 	Рюкзак мне больше не нужен, поэтому, высыпав все содержимое, я заталкиваю его под пассажирское сиденье. & рюкзакубрал = 1
вес = 0
Use_рюкзак_Освободить рюкзак_hide = 1
if патрубок = 2 and продукты = 1 and вода = 2 and сполок = 1 then pln & pln 	Все. Бак полон, продукты и вода из магазина собраны. Можно выдвигаться. & Use_машина_Ехать дальше_hide = 0
end

:Use_Стол_Осмотреть
pln 	Непримечательный серый стол с кассовым аппаратом, компьютером, рассыпанными канцелярскими принадлежностями и тетрадью с клетчатой обложкой. В углу под потолком висит небольшой пыльный телевизор.
inv_Тетрадь = 1
виделтетрадь = 1
proc события
end

:Use_Зеркало_Осмотреть
if Use_рюкзак_Надеть респиратор_hide = 0 then pln 	Я оглядываю многодневную щетину человека с той стороны стекла. Он делает то же самое. & pln 	"Побрейся уже," - говорим мы друг другу.
if Use_рюкзак_Надеть респиратор_hide = 1 then pln 	Я рассматриваю зубастую улыбку, нарисованную маркером на респираторе человека, стоящего по ту сторону стекла. Он оглядывает мою. & pln 	"Я первый придумал," - говорим мы друг другу.
proc события
end

:Use_Календарь_Осмотреть
pln 	Сверху - фотография серфингистки, скользящей под нависающей волной.
pln 	Ниже - таблица чисел с красной рамкой, установленной на дату #день3$ апреля. Шесть месяцев назад.
proc события
end

:Use_План эвакуации_Осмотреть
decor_plan_z = -1
decor_plan_hide = 0
decorcol plan 0xffffffff, 200
pause 200
anykey
decorcol plan 0x01ffffff, 200
pause 200
decor_plan_hide = 1
end

:Use_Тетрадь_Вкладыш
pln 	Ламинированный листок, отпечатанный на розовой бумаге.
pln 
textalign = 3
pln 	#день1$ апреля, пн    --   #%код1$
pln 	#день2$ апреля, вт    --   #%код2$
pln 	#день3$ апреля, ср    --   #%код3$
pln 	#день4$ апреля, чт    --   #%код4$
pln 	#день5$ апреля, пт    --   #%код5$
textalign = 1
proc события
end

:Use_Тетрадь_Журнал замеров
pln 	Записанные результаты замеров уровня топлива в подземном резервуаре.
pln 
textalign = 3
textfont = "segoe.fnt"
pln 	Дата   //   Время   //   Уровень
pln 	04.#день1$   //   10:02   //   ~ 1105 гал.
pln 	04.#день2$   //   10:01   //   ~ 1104 гал.
pln 	04.#день3$   //   11:38   //   ~ 487 гал.
textfont = "_sysfont"
textalign = 1
proc события
end

:Use_Тетрадь_Страница 1
textfont = "segoe.fnt"
pln 	#день1$ апреля, понедельник
pln 
pln 	Все-таки отключили электричество. Пришлось вывесить объявления о том, что колонки не работают. Правда, проезжающих по шоссе стало совсем мало.
pln 	Лиза жалуется, что в магазине и на складе начали портиться продукты и запах просто кошмарный. Сидим целый день здесь, играем в покер и костерим снабженческий отдел, не поставивший нам автономный генератор.
pln 	Самое дурацкое, что по правилам компании нужно ежедневно контролировать количество топлива в резервуарах. Теперь мне придется каждое утро ковыряться с замерным патрубком. Вырвала из "розовой" инструкции (это которая на чрезвычайные ситуации) листок с комбинациями для замка на эту неделю и уже сделала первую запись в журнал.
textfont = "_sysfont"
proc события
end

:Use_Тетрадь_Страница 2
textfont = "segoe.fnt"
pln 	#день2$ апреля, вторник
pln 
pln 	Лиза не приехала. Вчера под вечер жаловалась на головную боль и уехала пораньше. Наверное, из-за жары. Сомневаюсь, что она заболела. От кого бы?
pln 	Проверила уровень топлива. Естественно, не изменился. Но правила есть правила. Чуть не ошиблась, выставляя комбинацию на завтра.
pln 	Задерживая дыхание, натаскала из магазина печенья и газировки. Сижу одна в духоте, читаю Гомера - через месяц тест по античной литературе.
textfont = "_sysfont"
proc события
end

:Use_Тетрадь_Страница 3
textfont = "segoe.fnt"
pln 	#день3$ апреля, среда
pln 
pln 	Вчера не смогла уехать домой. Как это ни смешно, кончился бензин. Сижу тут на бочке с топливом и не могу заправить свою машину. Финиш.
pln 	Повезло, что проезжали военные. Внедорожник, пара тентованных грузовиков и - там-тарам! - прицепная дизель-электростанция. Они ее как-то куда-то подключили и запустили пару колонок. Высосали себе галлонов шестьсот и залили мне где-то полбака. Главный у них, правда, как-то грустно ухмыльнулся, когда я подала ему для подписи расписку о выдаче топлива. Но подписал и посоветовал закрыть станцию и ехать домой, потому как через день-другой могут ввести чрезвычайное положение. На мои дальнейшие расспросы отвечать отказался. Потом они погрузились на машины и укатили на восток.
pln 	Вобщем, я решила воспользоваться советом, сделала напоследок замер, выставила завтрашнюю комбинацию на замке, сейчас все закрою, опущу ставни и поеду в город, навещу Лизу.
pln 	Надеюсь, до скорого!
textfont = "_sysfont"
proc события
end

:Use_патрубок_Осмотреть
if патрубок = 0 then pln 	Труба диаметром в три дюйма, через которую я намереваюсь откачать себе горючее. В данный момент надежно закрыта крышкой со встроенным замком. & pln 	Комбинация к замку выставляется вращением кольца с цифрами. Попеременно поворачивая его то по часовой стрелке, то против, нужные цифры совмещаются с риской на неподвижной части крышки.
if патрубок = 1 then pln 	Труба диаметром в три дюйма, через которую я намереваюсь откачать себе горючее.
if патрубок = 2 then pln 	Труба диаметром в три дюйма, через которую я откачал себе бензин.
proc события
end

:Use_патрубок_Вращать по часовой стрелке
if вращение = 1 then pln 	Я замечаю, что поворачиваю крышку в ту же сторону, что и в прошлый раз. Комбинацию придется выставлять заново. & вращение = 0 & код = "" else proc наборкода & вращение = 1
proc события
end

:Use_патрубок_Вращать против часовой стрелки
if вращение = 2 then pln 	Я замечаю, что поворачиваю крышку в ту же сторону, что и в прошлый раз. Комбинацию придется выставлять заново. & вращение = 0 & код = "" else proc наборкода & вращение = 2
proc события
end

:Use_патрубок_Начать откачивать бензин
goto откачка
end

:Use_Газеты_Осмотреть
decor_shop_text = "	Кричащие, набранные крупным шрифтом заголовки - новости, устаревшие на несколько месяцев. Одна из передовиц:#/$#/$"
заголовок = rnd8
if заголовок = 1 then decor_shop_text = decor_shop_text + "''Белый дым над Сикстинской капеллой. Несмотря ни на что, Конклав избрал нового Папу.''"
if заголовок = 2 then decor_shop_text = decor_shop_text + "''Следуя примеру Японии, Израиль закрывает свои границы и возвращает дипломатические миссии.''"
if заголовок = 3 then decor_shop_text = decor_shop_text + "''Пир во время чумы. Треть противовирусных препаратов на рынке - пустышки.''"
if заголовок = 4 then decor_shop_text = decor_shop_text + "''Кто вы, мистер Смерть? Эксперты сообщают, что нынешний штамм - комбинация двух предыдущих.''"
if заголовок = 5 then decor_shop_text = decor_shop_text + "''Истребление кошек Коллизея. Власти Рима вынуждены уничтожить кошачью колонию, долгие годы обитавшую в архитектурном памятнике, из-за угрозы распространения вируса.''"
if заголовок = 6 then decor_shop_text = decor_shop_text + "''Почему шестой уровень угрозы был введен так поздно?''"
if заголовок = 7 then decor_shop_text = decor_shop_text + "''Беспорядки у штаб-квартиры ВОЗ в Женеве. Количество пострадавших растет.''"
if заголовок = 8 then decor_shop_text = decor_shop_text + "''Мир качается на грани эпидемии, которая может уничтожить большую часть человеческой популяции''#/$Роберт Вебстер, 2003 год''"
end

:Use_Холодильники_Обыскать
if вода = 2 then decor_shop_text = "В холодильниках осталась одна газировка. Всю воду я уже забрал." & end
if вес = 0 and вода = 0 then decor_shop_text = "По очереди открываю холодильники и загружаю в рюкзак всю воду.#/$Рюкзак полон." & вес = 2 & вода = 2 & end
if вес = 0 and вода = 1 then decor_shop_text = "Открываю один из холодильников и загружаю в рюкзак оставшуюся воду.#/$Рюкзак заполнен наполовину." & вес = 1 & вода = 2 & end
if вес = 1 and вода = 0 then decor_shop_text = "Открываю один из холодильников и загружаю оставшийся объем рюкзака бутылками с водой. Нужно будет вернуться, как разгружу рюкзак, и забрать остальное." & вес = 2 & вода = 1 & end
if вес = 1 and вода = 1 then decor_shop_text = "Открываю последний холодильник и загружаю в рюкзак оставшуюся воду.#/$Рюкзак полон." & вес = 2 & вода = 2 & end
if вес = 2 then decor_shop_text = "Открываю один из холодильников и смотрю на прозрачные бутылки с водой. Но в рюкзаке места уже нет, нужно сначала его разгрузить." & end

:Use_Полки_Обыскать
if сполок = 1 then decor_shop_text = "Больше на полках нет ничего для меня нужного." & end
if вес = 0 then decor_shop_text = "Сгребаю с полок в рюкзак сигареты, зажигалки, разнокалиберные батарейки, все, что может хоть как-то пригодиться.#/$Рюкзак заполнен наполовину." & вес = 1 & сполок = 1 & end
if вес = 1 then decor_shop_text = "Сгребаю с полок в рюкзак сигареты, зажигалки, разнокалиберные батарейки, все, что может хоть как-то пригодиться.#/$Рюкзак полон." & вес = 2 & сполок = 1 & end
if вес = 2 then decor_shop_text = "Высматриваю на полках сигареты, батарейки, разный мелкий товар, который может пригодиться. Но сначала нужно разгрузить рюкзак." & end

:Use_Продуктовые стеллажи_Обыскать
if продукты = 1 then decor_shop_text = "Все, что мне было нужно, я уже собрал." & end
if вес = 0 then decor_shop_text = "Внимательно проверяю сроки годности и условия хранения на упаковках продуктов и сгребаю подходящие в рюкзак. Консервы, каши, галеты, сублимированное мясо. Здесь же обнаруживаю зубные пасты, бумажные полотенца, одноразовые бритвенные станки, автомобильные аптечки. Сгребаю.#/$Рюкзак заполнен наполовину." & вес = 1 & продукты = 1 & end
if вес = 1 then decor_shop_text = "Внимательно проверяю сроки годности и условия хранения на упаковках продуктов и сгребаю подходящие в рюкзак. Консервы, каши, галеты, сублимированное мясо. Здесь же обнаруживаю зубные пасты, бумажные полотенца, одноразовые бритвенные станки, автомобильные аптечки. Сгребаю.#/$Рюкзак полон." & вес = 2 & продукты = 1 & end
if вес = 2 then decor_shop_text = "С удовлетворением разглядываю разнообразие продуктов на полках. Но сначала нужно разгрузить рюкзак." & end

:Use_Стеллажи с инструментами_Обыскать
if естькомпресс = 1 then decor_shop_text = "Больше мне из инструмента ничего не нужно."
if естькомпресс = 0 then decor_shop_text = "Разглядывая лежащие на полках и висящие на стене инструменты, я замечаю автомобильный компрессор и у меня появляется идея, как откачать топливо из резервуара. Проверив его, с удовлетворением узнаю, что компрессор не только может накачать шину, но и снабжен переключателем для быстрой откачки воздуха - именно это мне и нужно. Беру его с собой." & inv+ Компрессор
if естькомпресс = 0 and естьшланг = 0 then decor_shop_text = decor_shop_text + "#/$Для воплощения идеи в жизнь осталось найти шланг нужной длины."
if естькомпресс = 0 then естькомпресс = 1
end

:Use_Шланг_Взять
if естькомпресс = 0 then decor_shop_text = "Прикинув длину шланга и решив, что ее хватит, чтобы достать до уровня топлива в резервуаре, я снимаю шланг с крючка и вешаю себе на плечо."
if естькомпресс = 1 then decor_shop_text = "Прикинув длину шланга и решив, что он подойдет для осуществления моего плана, я снимаю шланг с крючка и вешаю себе на плечо."
естьшланг = 1
idisp_Шланг = "Шланг"
Use_Шланг_Взять_hide = 1
proc осмотразс
end

:Use_Шланг_Осмотреть
if гденаазс < 8 then pln 	Гофрированный шланг в дюйм диаметром и длиной около пятнадцати футов. & end
if гденаазс > 7 then decor_shop_text = "Гофрированный шланг в дюйм диаметром и длиной около пятнадцати футов." & end
end

:Use_Шланг_Начать откачивать бензин
goto откачка
end

:Use_Компрессор_Осмотреть
if гденаазс < 8 then pln 	Автомобильный компрессор для подкачки шин. В инструкции говорится, что за 10 секунд накачивает шину R16 до двух атмосфер.#/$	Но для меня важнее, что он снабжен еще и функцией откачки воздуха. & end
if гденаазс > 7 then decor_shop_text = "Автомобильный компрессор для подкачки шин. В инструкции говорится, что за 10 секунд накачивает шину R16 до двух атмосфер.#/$Но для меня важнее, что он снабжен еще и функцией откачки воздуха." & end
end

:Use_Компрессор_Начать откачивать бензин
goto откачка
end

:Use_Заднее сиденье_Осмотреть
pln 	Все, что я набрал в магазине, свалено кучей на заднее сиденье. Разберу и рассортирую, когда остановлюсь на ночлег.
proc события
end

:Use_Заднее сиденье_Искать обезболивающее
pln 	Не съезжая на обочину, останавливаюсь прямо посреди шоссе и достаю с заднего сиденья одну из автомобильных аптечек, найденных в магазине. Снимаю пленку и открываю зеленую коробку с белым крестом. Порывшись в бинтах, пластырях и стерильных салфетках, нахожу пластиковую бутылочку "Тиленола №1". Отлично, парацетамол и немного кодеина -  то, что мне сейчас нужно. Глотаю две капсулы и запиваю водой.
pln 	Возвращаю все на заднее сиденье и, переключив передачу, вжимаю педаль газа.
anykey
pln
pln 	Когда стрелка спидометра переваливает за отметку "80", я замечаю у себя на коленях рекламный листок, выпавший из аптечки. Оглядев его, перед тем, как смять и выбросить, вижу хорошенькую медсестру, улыбающуюся безупречно белой улыбкой...
anykey
invkill
decorcol fade 0xffffffff, 2000
fademusic 0, 2000
;music stop, 2000
pause 2000
Use_Заднее сиденье_Искать обезболивающее_hide = 1
goto флешбэк3
end

;---------------------------------------------------------------------------------------------------

:сколько_сигарет
if сиг >= 17 then пачка = "почти полная"
if сиг > 12 and сиг < 17 then пачка = "больше дюжины сигарет"
if сиг = 12 then пачка = "еще дюжина сигарет"
if сиг < 12 and сиг > 10 then пачка = "меньше дюжины сигарет"
if сиг = 10 then пачка = "еще с десяток сигарет"
if сиг = 9 then пачка = "осталось девять сигарет"
if сиг = 8 then пачка = "осталось восемь сигарет"
if сиг = 7 then пачка = "осталось семь сигарет"
if сиг = 6 then пачка = "осталось полдюжины сигарет"
if сиг = 5 then пачка = "осталось пять сигарет"
if сиг = 4 then пачка = "осталось четыре сигареты"
if сиг = 3 then пачка = "осталось три сигареты"
if сиг = 2 then пачка = "осталось две сигареты"
if сиг = 1 then пачка = "осталась одна сигарета"
if сиг = 0 then пачка = "пуста"
end

:события
курение = курение + 1
пить = пить + 1
нагревание = нагревание + 1
флешбекбезтравки = флешбекбезтравки + 1
голова = голова - 1
if курение = 4 and вмашине = 1 then pln & pln 	Докурив сигарету, я раздавливаю окурок в выдвижной пепельнице под приемником.
if курение = 2 and вмашине = 1 and вдвижении = 1 then pln & pln 	Сигаретный дым уносится ветром через опущенное стекло.
if курение = 4 and вмашине = 0 and былфлешбек = 1 then pln & pln 	Докурив сигарету, я роняю окурок и раздавливаю его носком ботинка.
if курение = 4 and вмашине = 0 and былфлешбек = 2 and Use_рюкзак_Снять респиратор_hide = 1 then pln & pln 	Докурив сигарету, я растираю окурок о подошву ботинка и только потом бросаю его на землю.
if пить = 4 and вдвижении = 1 then pln & pln 	В горле совсем пересохло. Нужно останавливаться.
if пить = 6 and вдвижении = 1 then pln & pln 	Пить хочется невыносимо. & goto Use_машина_Oстановиться
if нагревание = 3 and вдвижении = 0 and вмашине = 1 then pln & pln 	Внутри, несмотря на опущенные стекла, становится жарко.
if нагревание = 5 and вдвижении = 0 and вмашине = 1 then pln & pln 	Салон Транс-Эма все больше напоминает духовку.
if флешбекбезтравки = 3 and inv_пакет = 0 then pln & pln 	Я вдруг вспоминаю мотоциклетный бак на плоском валуне. Установленный посреди безмолвной степи памятник самоубийственному легкомыслию. Памятник всему человечеству... &;
_anykey &;
_invkill &;
_decorcol fade 0xffffffff, 2000 &;
_fademusic 0, 2000 &;
;_music stop, 2000 &;
_pause 2000 &;
_goto флешбэк2
if вес = 2 and гденаазс < 7 then усталость = усталость + 1
if вес = 0 then усталость = 0
if усталость = 5 and вмашине = 0 then pln & pln 	Начинаю чувствовать, как лямки рюкзака врезаются в плечи. Пора его разгружать.
if голова = 3 then pln & pln 	Чувствую, как начинает болеть голова. Должно быть, слишком много времени провел на солнце. & Use_Заднее сиденье_Искать обезболивающее_hide = 0
if голова = 1 then pln & pln 	Голова болит все сильнее. Нужно проверить аптечки из магазина, там должно быть что-то обезболивающее.
end

:осмотразс
pln
if гденаазс = 1 and машинанаазс = 1 then pln 	Здесь, в тени под навесом, куда не попадают опаляющие лучи солнца, сохраняется хоть какое-то подобие прохлады. Слабый ветер лениво метет по асфальтированной площадке поземку из песка, огибая колеса остывающего Транс-Эма. & pln 	У трех белых столбов, поддерживающих навес, стоят по две колонки. На каждой висит табличка "Не обслуживается". & pln 	Главный фасад одноэтажного магазина, который, скорее всего, является и офисом, почти полностью закрыт белыми металлическими рольставнями.
if гденаазс = 1 and машинанаазс = 2 then pln 	Здесь, в тени под навесом, куда не попадают опаляющие лучи солнца, сохраняется хоть какое-то подобие прохлады. Слабый ветер лениво метет по асфальтированной площадке поземку из песка. & pln 	У трех белых столбов, поддерживающих навес, стоят по две колонки. На каждой висит табличка "Не обслуживается". & pln 	Главный фасад одноэтажного магазина, который, скорее всего, является и офисом, почти полностью закрыт белыми металлическими рольставнями.
if гденаазс = 2 then pln 	Три секции запертых ставней, набранных из узких металлических полос, полностью закрывают витрины магазина и главный вход. Замки в таких ставнях не открыть без ключа или профессиональной отмычки. Ни того, ни другого у меня нет, значит, попасть внутрь отсюда не получится. & pln 	На средней секции висит записка.
if гденаазс = 3 then pln 	В восточной стене здания нет ни дверей, ни окон, только пара кондиционеров, не издающих ни звука.
if гденаазс = 4 and окноразбито = 0 then pln 	Западная стена ярко освещена солнцем и слепит своей белизной. Единственное окно завешено изнутри жалюзи.
if гденаазс = 4 and окноразбито = 1 then pln 	Западная стена ярко освещена солнцем и слепит своей белизной. Под единственным окном поблескивают осколки стекла. Легкий ветер едва колышит серые жалюзи.
if гденаазс = 5 /*and складоткрыт = 0*/ then pln 	Над южной стеной висит метровый козырек, защищающий от солнца. Широкие подъемные ворота, скорее всего, служили для приемки товара - за ними склад. Справа от ворот расположена выкрашенная в красный цвет дверь. Над дверью тоскливо постукивает деревянный ловец ветра.
/*if гденаазс = 5 and складоткрыт = 1 then pln 	Над южной стеной висит метровый козырек, защищающий от солнца. Широкие подъемные ворота, скорее всего, служили для приемки товара, значит, за ними должен быть склад. Справа от ворот расположена выкрашенная в красный цвет дверь с выломанным замком. Над дверью тоскливо постукивает деревянный ловец ветра.*/
if гденаазс = 6 and ящикоткрыт = 0 then pln 	На прямоугольном постаменте установлена махина электрического насоса с несколькими выводами для подключения шлангов от автоцистерн. В стороне стоит неприметный серый ящик с надписью "Замерный патрубок", запертый на висячий замок.
if гденаазс = 6 and ящикоткрыт = 1 then pln 	На прямоугольном постаменте стоит махина электрического насоса с несколькими выводами для подключения шлангов от автоцистерн. В стороне стоит серый ящик, защищающий замерный патрубок. Рядом лежат части сломанного замка.
if гденаазс = 6 and компресснапункте = 1 and шлангнапункте = 0 and патрубок <> 2 then pln 	В тени у насоса стоит компрессор.
if гденаазс = 6 and компресснапункте = 0 and шлангнапункте = 1 and патрубок <> 2 then pln 	В тени у насоса лежит шланг.
if гденаазс = 6 and компресснапункте = 1 and шлангнапункте = 1 and патрубок <> 2 then pln 	В тени у насоса оставлены шланг и компрессор.
if гденаазс = 6 and машинанаазс = 2 then pln 	Над крышей и капотом стоящего на солнце Транс-Эма клубится марево горячего воздуха.
if гденаазс = 6 and машинанаазс = 2 and компресснапункте = 1 and шлангнапункте = 1 and патрубок <> 2 then pln & pln 	Я нашел шланг и компрессор и подогнал Транс-Эм, так что все готово к тому, чтобы начать откачивать бензин. & Use_патрубок_Начать откачивать бензин_hide = 0 & Use_Шланг_Начать откачивать бензин_hide = 0 & Use_Компрессор_Начать откачивать бензин_hide = 0
if гденаазс = 7 and окноразбито = 1 then pln 	Небольшая комната отдыха для персонала и, одновременно, место работы оператора. Стол с компьютером и микрофоном стоит у северной стены под окном, закрытым с внешней стороны рольставнями. Ветер колышит жалюзи второго, разбитого окна в западной стене. Стоящая рядом софа засыпана осколками. Слева от двери, ведущей в магазин, висят зеркало и календарь. Справа - план эвакуации.
if гденаазс = 7 and окноразбито = 0 then pln 	Небольшая комната отдыха для персонала и, одновременно, место работы оператора. Стол с компьютером и микрофоном стоит у северной стены под окном, закрытым с внешней стороны рольставнями. Под вторым окном, у западной стены, стоит софа. Слева от двери, ведущей в магазин, висят зеркало и календарь. Справа - план эвакуации.
if гденаазс = 8 and естьшланг = 0 then описание = "Длинное помещение, заставленное пустыми ящиками из-под товара. В его западном конце расположена холодильная камера. Запах оттуда жуткий, чувствуется даже сквозь респиратор.#/$	Две двери напротив друг друга, одна на улицу, вторая в магазин. На восточной стене висит свернутый шланг."
if гденаазс = 8 and естьшланг = 1 then описание = "Длинное помещение, заставленное пустыми ящиками из-под товара. В его западном конце расположена холодильная камера. Запах оттуда жуткий, чувствуется даже сквозь респиратор.#/$	Две двери напротив друг друга, одна на улицу, вторая в магазин."
if гденаазс = 9 then описание = "Стеллажи рядом с витриной до самого потолка заставлены разнообразными продуктами, в большинстве испорченными, но есть и с большим сроком хранения. #/$ Сквозь витрину видна обратная сторона рольставней."
if гденаазс = 10 then описание = "Дверь, ведущая на склад, чуть приоткрыта. У восточной стены стоит стеллаж с инструментом. Дальше тянутся стеллажи с продуктами."
if гденаазс = 11 then описание = "Сквозь прозрачную входную дверь и витрину видно обратную сторону закрытых рольставней. На конторке, за которой раньше сидел продавец, стоит бесполезный кассовый аппарат. На стене за ней висят полки с разнообразным мелким товаром. Рядом - стойка с газетами."
if гденаазс = 12 then описание = "Несколько неработающих холодильников с водой и газировкой стоят в ряд у стены. Дверь с надписью ''Для персонала''. Рядом с дверью - тянущаяся вдоль западной стены в темноту конторка."
proc события
end

:датаикод
день1 = 2+rnd3*7
день2 = #день1$+1
день3 = #день1$+2
день4 = #день1$+3
день5 = #день1$+4
instr код1 = #rnd8999+1000$
instr код2 = #rnd8999+1000$
instr код3 = #rnd8999+1000$
instr код4 = #rnd8999+1000$
instr код5 = #rnd8999+1000$
end

:наборкода
pln 	Я поворачиваю кольцо крышки до цифры:
instr цифра = 
input цифра
if цифра = "0" or цифра = "1" or цифра = "2" or цифра = "3" or цифра = "4" or цифра = "5" or цифра = "6" or цифра = "7" or цифра = "8" or цифра = "9" then код = "#%код$#%цифра$" else pln 	Задумавшись, проворачиваю крышку на несколько оборотов. Комбинацию придется выставлять заново. & код = "" & вращение = 0
if код == "*#%код4$" then pln & pln 	Раздается щелчок и я с легкостью снимаю крышку. В воздухе разносится многообещающий запах бензина. Можно перегонять Транс-Эм. & патрубок = 1 & idisp_патрубок = "Патрубок" & Use_патрубок_Вращать по часовой стрелке_hide = 1 & Use_патрубок_Вращать против часовой стрелки_hide = 1
if код == "*#%код3$" then pln & pln 	Хм. Комбинация не подошла. Должно быть, я что-то упустил.
if код == "*#%код4$" and естьшланг = 0 and естькомпресс = 0 then pln 	Но есть пара проблем. Первая, уровень топлива в резервуаре слишком низкий и у меня нет шланга достаточной длины. Вторая, старый добрый метод добычи бензина - отсосом - здесь не подойдет, тоже из-за большой разницы в высотах. Нужно что-то придумывать.
if код == "*#%код4$" and естьшланг = 1 and естькомпресс = 0 then pln 	Но есть проблема - слишком низкий уровень топлива в резервуаре. Я нашел шланг достаточной длины, только старый добрый метод добычи бензина - отсосом - уже не подойдет. Нужно что-то придумывать.
if код == "*#%код4$" and естьшланг = 0 and естькомпресс = 1 then pln 	Но есть проблема - слишком низкий уровень топлива в резервуаре. Я нашел компрессор, которым при должной ловкости можно откачать бензин, а вот шланга достаточной длины у меня нет. Нужно где-то искать.
end

:входвмагазин
cls
fademusic 0, 500
proc осмотразс
decorcol back 0x000000, 200
pause 200
decor_shop_text = описание
decor_shop_hide = 0
hide_inv_echo = 1
inv- Стол
inv- Тетрадь
inv- Зеркало
inv- Календарь
inv- План эвакуации
Use_азс_Пройти в магазин_hide = 1
Use_азс_Вылезти через окно_hide = 1
Use_азс_Обойти здание с восточной стороны_hide = 1
Use_азс_Обойти здание с западной стороны_hide = 1
Use_азс_Идти к пункту налива_hide = 1
Use_азс_Идти на склад_hide = 1
Use_азс_Осмотреться_hide = 0
Use_азс_Идти в темноту_hide = 0
if гденаазс = 8 then Use_азс_Выйти на улицу_hide = 0
if гденаазс = 12 then Use_азс_Войти в дверь_hide = 0
if былсветвмагазине = 1 then proc Use_рюкзак_Включить фонарь & decor_shop_text = "	Сразу же включаю фонарь.#/$#/$" + описание
textcolor = 0x554f36
знаниеозапахе = 1
end

:откачка
патрубок = 2
cls
pln 	Я разматываю шланг и опускаю один его конец в патрубок. Открываю крышку бензобака. Подключаю компрессор к прикуривателю и перевожу его в режим откачки. Нажимаю кнопку включения.
pln 	Стрекочущее тарахтенье эхом разносится по округе, когда компрессор начинает работать. Держа свободный конец шланга в одной руке, я прижимаю к нему свистящий штуцер на конце трубки, идущей от компрессора. По вибрации шланга понимаю, что жидкость начинает высасываться наверх.
pln 	Через несколько секунд я вижу, как бензин устремляется по шлангу от патрубка к концу, и едва успеваю отдернуть руку со штуцером. Желтоватая жидкость выплескивается наружу и я, заливая крыло Транс-Эма, вставляю шланг в горловину бензобака.
pln
pln 	Через некоторое время просто вытягиваю шланг из патрубка - напор пропадает. Закрываю бензобак, вытряхиваю остатки бензина из шланга, сворачиваю его, отсоединяю и собираю компрессор и заталкиваю все в багажник. Готово.
pln
inv- Шланг
inv- Компрессор
Use_патрубок_Начать откачивать бензин_hide = 1
if продукты = 1 and вода = 2 and сполок = 1 then pln 	Теперь можно ехать. & end
if продукты <> 1 or вода <> 2 or сполок <> 1 then pln 	Осталось проверить, что еще можно собрать в магазине, и можно ехать. & end
if продукты <> 1 and вода <> 2 and сполок <> 1 then pln 	Осталось проверить, что можно собрать в магазине, и можно ехать. & end
end

;---------------------------------------------------------------------------------------------------

:конец
proc фото("1.JPG", 333, 184, 100)
proc фото("2.JPG", 334, 188, 99)
proc фото("3.JPG", 353, 136, 98)
goto конец
end

:фото
икс = rnd#760-#фото_2$$ + 20
игрек = rnd#425-#фото_3$$ + 55
decoradd фотодек (икс, игрек, фото_4) image фото_1
decorcol фотодек 0x01ffffff
decorcol фотодек 0xffffffff, 3000
pause 8000
decorcol фотодек 0x01ffffff, 3000
pause 3000
decordel фотодек
end

:отавтора
decorcol back 0xff000000
decoradd about (100, 100, -10) text "собственно, текст", 0xdddddd
btn назад, Назад
btn сначала, Сначала
end

:назад
decorcol back 0x01000000
decordel about
btn отавтора, От автора
btn сначала, Сначала
end

:сначала
btn отавтора, От автора
btn сначала, Сначала
decoradd last (0,0,-20) rect 800,600,0x01000000
decorcol last 0xff000000, 2000
fademusic 1, 2000
pause 2000
invkill
perkill
decordel
goto init
end











   